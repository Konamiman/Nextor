	title	Nextor SDK - EXTNUM

	.Z80

;-----------------------------------------------------------------------------
;
; Extract a 5 digit number from a string
;
; Input:    HL = ASCII string address
; Output:   CY-BC = 17 bit number
;           D  = Count of digits of the number.
;                The number is considered to be extracted
;                when a non-numeric character is found,
;                or when five digits have been extracted.
;           E  = First non-numeric character (o 6th digit)
;           A  = error code:
;                0 => Success
;                1 => The number has more than 5 digits.
;                     CY-BC contains then the number built from
;                     the first 5 digits.
; Modifies: -

	ifrel
	public EXTNUM
	endif

EXTNUM:
	push	hl
	push	ix
	ld	ix,ACA
	res	0,(ix)
	set	1,(ix)
	ld	bc,0
	ld	de,0
BUSNUM:	ld	a,(hl)	;Jump to FINEXT if not a digit, or is the 6th digit
	ld	e,a
	cp	"0"
	jr	c,FINEXT
	cp	"9"+1
	jr	nc,FINEXT
	ld	a,d
	cp	5
	jr	z,FINEXT
	call	POR10

SUMA:	push	hl	;BC = BC + A
	push	bc
	pop	hl
	ld	bc,0
	ld	a,e
	sub	"0"
	ld	c,a
	add	hl,bc
	call	c,BIT17
	push	hl
	pop	bc
	pop	hl

	inc	d
	inc	hl
	jr	BUSNUM

BIT17:	set	0,(ix)
	ret
ACA:	db	0	;b0: num>65535. b1: more than 5 digits

FINEXT:	ld	a,e
	cp	"0"
	call	c,NODESB
	cp	"9"+1
	call	nc,NODESB
	ld	a,(ix)
	pop	ix
	pop	hl
	srl	a
	ret

NODESB:	res	1,(ix)
	ret

POR10:	push	de
	push	hl	;BC = BC * 10
	push	bc
	push	bc
	pop	hl
	pop	de
	ld	b,3
ROTA:	sla	l
	rl	h
	djnz	ROTA
	call	c,BIT17
	add	hl,de
	call	c,BIT17
	add	hl,de
	call	c,BIT17
	push	hl
	pop	bc
	pop	hl
	pop	de
	ret
