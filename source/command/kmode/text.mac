	.z80
;
	title	KMODE - Japanese MSX-DOS 2 utility
	subttl	Messages File (English/Japanese bilingual)
;
;        This is a messages file for the KMODE utility.   It contains messages
; and text in English/Japanese in the format required by the KMODE .REL files.
;
;        Care  should be taken when changing this file that spaces within each
; message are preserved. Any of the messages may be changed to a null string.
;
;
;        These messages are used by TPA by looking for the 'n'th string, where
; n  is  the  number  corresponding to the string required.  In all cases, the
; actual value of 'n' is defined by  a  label  defined  as  external  in  this
; module,  so  the  messages  can  be in any order. The strings themselves are
; ordinary ASCII strings terminated by a null (0).
;
;        A macro MSG is used here to define  the  strings  and  the  external
; lables.   It is used in the form: 1) MSG NAME,'<STRING>' or 2) MSG NAME. In
; the former case, an external lable of the for __NAME  with  an  appropriate
; value  is  declared,  and the string is defined with DEFBs with the null on
; the end. In the latter case, the label is still  defined  but  no  text  is
; defined.   This  allows following DEFBs to define the text, which may be on
; more than one line. Don't forget a DEFB 0 at the end though !.
;
;       The global symbol MESSAGES is the start of the messages.
;
;
;-----------------------------------------------------------------------------
;	GETMSG:  This routine is  used to find out the  absolute address of  a
; text message.
;
; ENTRY:    A  = message number.
; EXIT:     DE = absolute address of text message.
; CORRUPTS: AF & DE only.
;
GETMSG::
if 1
	push	af		; Save message number.
	xor	a
	ld	de,1100h
	call	0FFCAh		;FCALL
	pop	de		; D = message number.
	add	a,255
	ld	a,d		; A = message number * 2 if normal char set
	adc	a,a		;     message number * 2 + 1 otherwise.
endif
	LD	DE,MESSAGES
	OR	A
	RET	Z
;
	PUSH	BC
	LD	B,A
;
NXTM:	LD	A,(DE)
	INC	DE
	OR	A
	JR	NZ,NXTM
	DJNZ	NXTM
;
	POP	BC
	RET
;
;
;------------------------------------------------------------------------------
;
LF	equ	10
CR	equ	13
;
MSG	macro	NAME,TEXT
__&NAME	 equ	MSGNO		;; Define message number label.
global	 __&NAME		;; And declare it public.
MSGNO	 defl	MSGNO+1
	 ifnb	<TEXT>		;; Define the text, if given.
	  defb	TEXT,0
	 endif
	endm
;
ASCIZ	macro	TEXT
	 defb	TEXT,0
	endm
;
MSGNO	defl	0		;; First message must be number 0.
;
MESSAGES:			; Start of the messages.
;
;------------------------------------------------------------------------------
;
;        BADVER below is printed by KMODE if the DOS version is pre-2.00. It is
; designed to be in the same format as normal CLI errors.
;
;
	MSG	BADVER
	ASCIZ	<CR,LF,"*** Wrong version of MSX-DOS",CR,LF>
	ASCIZ	<CR,LF,"*** MSX-DOS のバージョンが違います",CR,LF>
;
;
;------------------------------------------------------------------------------
;
;        Before  performing  it's operation on a disk, KMODE prints a prompt in
; the form:
;
;               Disk in drive A: will only be able to boot MSX-DOS 2
;               \            / \                                   /
;                 ----a-----  b  --------------c------------------
;
;        DISK (part (a)) is printed first, followed by the drive letter  (part
; (b) of the disk which is to be updated. Then ONLY (part (c)) is printed.
;
;
	MSG	DISK
	ASCIZ	<"Disk in drive ">			; One trailing space.
	ASCIZ	<"ドライブ ">
;
	MSG	ONLY
	ASCIZ	<": will only be able to boot MSX-DOS 2">
	ASCIZ	<": のディスクは日本語 MSX-DOS2 しか立ち上げられなくなります">
;
;
;        Following the above text, the message PRAK below will be printed, and
; a key press waited for. This message is the same as that  used  for  similar
; prompting throughout the command interpreter and for the PAUSE command.
;
;
	MSG	PRAK
	ASCIZ	<"Press any key to continue... ">	; One trailing space.
	ASCIZ	<"何かキーを押して下さい ">
;
;
;
	END
