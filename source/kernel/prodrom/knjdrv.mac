	.z80
;
;	Entries for Kanji driver
;
;	L80 /P:40B4,KNJDRV,...
;
@MAPBIO::
	call	KNJBIO
	jp	MAPBIO##

@TIMINT::
	call	KNJTIM
	jp	TIMINT##

KNJVECT:
	call	KANJI		; Hook TOTEXT
	call	KANJI		; Hook CHPUT
	call	KANJI		; Hook DSPCSR
	call	KANJI		; Hook ERACSR
	call	KANJI		; Hook ERAFNK
	call	KANJI		; Hook DSPFNK
	call	KANJI		; Hook PINLIN
	call	KANJI		; Hook INLIN
	call	KANJI		; Hook LPTOUT
	call	KANJI		; Hook CHGET
	call	KANJI		; Hook WIDTH
	call	KANJI		; Hook SCREEN
;
KNJSTT::call	KANJI		; Kanji BASIC statement
KNJBIO::call	KANJI		; Kanji Extended BIOS
KNJTIM::call	KANJI		; Kanji Timer interrupt
;
KANJI:
	pop	ix
	push	bc
	ld	bc,REAL_KNJVECT-KNJVECT-3
				; -3 is because the IX points to the
				; return address
	ex	af,af'
	ld	a,3		; Bank number of Kanji driver
	add	ix,bc
	pop	bc
	jp	CALBNK##
;
	defs	KNJVECT+63-$,0FFh
;
; Just after this module follows BNK_ID byte.
; And, in Kanji BANK only, real jump vectors.
;
REAL_KNJVECT	equ	$+1
;
;
	end
