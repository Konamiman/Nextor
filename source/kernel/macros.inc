	.xlist
;
FALSE   equ     0
TRUE    equ     NOT FALSE
??????  equ     0
;
;
;

;Macros for generating IXh/IXl/IYh/IYl instructions.
;These instructions are generated by
;appending a DDh byte to instructions referring to H, to access IXh,
;or to instructions referring to L, to access IXl.
;For IY, FDh is used instead of DDh.

ld_iyh_a	macro
	db	0FDh
	ld	h,a
	endm

ld_iyl_a	macro
	db	0FDh
	ld	l,a
	endm

ld_iyh_b	macro
	db	0FDh
	ld	h,b
	endm

ld_iyh_c	macro
	db	0FDh
	ld	h,c
	endm

ld_a_iyh	macro
	db	0FDh
	ld	a,h
	endm

ld_a_iyl	macro
	db	0FDh
	ld	a,l
	endm

ld_c_ixh	macro
	db	0DDh
	ld	c,h
	endm

ld_ixh_a	macro
	db	0DDh
	ld	h,a
	endm

ld_a_ixh	macro
	db	0DDh
	ld	a,h
	endm

ld_a_ixl	macro
	db	0DDh
	ld	a,l
	endm

ld_a_iyh	macro
	db	0FDh
	ld	a,h
	endm

or_ixl	macro
	db	0DDh
	or	l
	endm

or_iyl	macro
	db	0FDh
	or	l
	endm

cp_ixl	macro
	db	0DDh
	cp	l
	endm

cp_ixh	macro
	db	0DDh
	cp	h
	endm

cp_iyl	macro
	db	0FDh
	cp	l
	endm

cp_iyh	macro
	db	0FDh
	cp	h
	endm


const   macro   name,value
name     equ    (value)
	 public name
	endm
;
;
;
warn    macro   text
	 if1
	  .printx % text
	 endif
	endm
;
;
;
error   macro   text
	 if1
	  .printx % text
	 endif
	endm
;
;
;
pr_dec  macro   msg1,value,msg2
	 warn    <msg1 value msg2>
	endm
;
;
pr_hex  macro   msg1,value,msg2
	 warn    <msg1 value!h msg2>
	endm
;
;
code    macro   instr,arg
	.8080
	 db      (instr arg)
	.z80
	endm
;
;
;-----------------------------------------------------------------------------
;
OFFSET  equ     4100h
PHASED  defl    FALSE
;
rammod  macro
PHASED   defl    TRUE
	 .phase  $-OFFSET
	endm
;
;
start:
;
finish  macro   name
	 if      PHASED
	  .dephase
	 endif
	 if1
	  pr_dec  <Size of module "&name" is>,%($-start),<bytes>
	 endif
	endm
;
;

;--- The "proc" macro is used to define a label that is invoked
;    at RAM in page 0 (when PHASED) or at ROM bank 4 (when not PHASED).

proc    macro   name                    ;;Macro for declaring a public
	if	PHASED
name&:                                  ;; label to be called.
	 .dephase
?&name&::
	 .phase  $-OFFSET
	else
name&:		;;For local call
?&name&::	;;For external call
	endif
	endm
;
;
;
pcall   macro   cc,name                 ;;Macro for calling an external
					;; routine.
	 ifb    <name>
	  call   ?&cc&##-OFFSET         ;;Unconditional CALL.
	 else
	  call   cc,?&name&##-OFFSET    ;;Conditional CALL.
	 endif
	endm
;
;
;

; STROUT implementation with the ESC-Y bug corrected.
; It is declared as as macro because it is defined
; twince in the kernel (bdos.mac, char.mac)
; and once more in MSXDOS2.SYS.
;
; OUTDIR is the routine used to print a character.
; RETDIR is the routine jumped to at end. If 0, ret is generated instead.
; LOADCE: if specified, E and C registers are loaded prior to calling CONOUT.

do_strout	macro	OUTDIR,RETDIR,LOADCE
	local	stro_init
	local	STROU2
	local	ST10

stro_init:
	LD	A,(DE)
	INC	DE
	cp	ESC
	jr	nz,STROU2

	call	ST10		;Print 'ESC'
	ld	a,(DE)
	inc	de
	cp	'Y'
	jr	nz,STROU2

	call	ST10		;Print 'Y'
	ld	a,(DE)
	inc	de
	call	ST10		;Print X coordinate
	ld	a,(DE)
	inc	de
	call	ST10		;Print Y coordinate

	jr	stro_init

STROU2:
	CP	'$'	
	ifidn <RETDIR>,<0>
	RET	Z
	else	
	JP	Z,&RETDIR&
	endif
	call	ST10
	JR	stro_init

ST10:	PUSH	DE

	ifnb	<LOADCE>
	LD	E,A
	LD	C,_CONOUT##
	endif

	CALL	&OUTDIR&
	POP	DE

	RET

	endm

;------------------------------------------------------------------------------
;
	.list
;
